def javaVersion    = '1.8';
def projectVersion = '0.9.3';

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.2"
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'java'

    project.group = 'org.develnext.jphp'
    project.version = projectVersion
    project.ext.isSnapshot = !Boolean.getBoolean("release");

    if (project.ext.isSnapshot) {
        version += '-SNAPSHOT'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task sourcesJar(type: Jar, dependsOn: 'classes') {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    configurations {
        provided
        java9
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
    }
}

configure(subprojects) {
    apply plugin: 'java'

    if (it.name.endsWith("-ext")) {
        apply plugin: 'maven'
    }

    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    jar {
        manifest.attributes(
                'Multi-Release': 'true'
        )
    }

    sourceSets {
        main.java.srcDirs = ['src/main/java']
        main.resources.srcDirs = ['src/main/resources']

        test.java.srcDirs = ["src/main/tests"]
        test.resources.srcDirs = ["src/main/tests"]
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    def extDir = "exts/";

    if (it.name.startsWith(extDir)) {
        def pr = it

        install {
            repositories.mavenInstaller {
                pom.artifactId = pr.name.substring(extDir.length())
            }
        }

        def installer = install.repositories.mavenInstaller

        [installer]*.pom*.whenConfigured {pom ->
            pom.dependencies.each { dep ->
                if (dep.artifactId.startsWith(extDir)) {
                    dep.artifactId = dep.artifactId.substring(extDir.length())
                }
            }
        }
    }
}
