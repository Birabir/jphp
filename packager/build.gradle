apply plugin: 'maven'

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['src-php']
}

dependencies {
    compile project(':jphp-core')
    compile project(':exts/jphp-zend-ext')
    compile project(':exts/jphp-yaml-ext')
    compile project(':exts/jphp-compress-ext')
    compile project(':exts/jphp-json-ext')
    compile project(':exts/jphp-httpserver-ext')
    compile project(':exts/jphp-semver-ext')
    compile project(':exts/jphp-text-ext')
    compile project(':exts/jphp-httpclient-ext')
}

shadowJar {
    archiveName = 'packager-all.jar'

    mergeServiceFiles()
}

task dist(dependsOn: 'shadowJar') {
    doLast {
        delete "$buildDir.path/dist/update"
        delete "$buildDir.path/dist/vendor"

        delete fileTree("$buildDir.path/dist/") {
            include 'jppm-*.tar.gz'
            include 'jppm-*.json'
            include 'update/'
        }

        copy {
            from "$buildDir.path/libs/$shadowJar.archiveName"
            from "$projectDir.path/scripts/"

            into "$buildDir.path/dist/"
        }

        copy {
            from "$projectDir.path/package.php.yml"
            from "$projectDir.path/package.github.yml"
            into "$buildDir.path/dist/"
        }

        copy {
            from "$projectDir.path/buildSrc/"
            into "$buildDir.path/dist/buildSrc"
        }
    }
}


task distArch(type: Tar, dependsOn: ['clean', 'dist', 'distArchZip']) {
    from("$buildDir.path/dist") {
        exclude('jppm')
    }

    from("$buildDir.path/dist") {
        include('jppm')

        fileMode = 0755
    }

    baseName = "jppm-dist"
    destinationDir = buildDir
    extension = 'tar.gz'

    compression = Compression.GZIP
}

task distArchZip(type: Zip, dependsOn: 'dist') {
    from("$buildDir.path/dist")

    baseName = "jppm-dist"
    destinationDir = buildDir
    extension = 'zip'
}

task publish(type: Exec, dependsOn: 'dist') {
    def osName = System.getProperty("os.name").toLowerCase()
    workingDir "$buildDir.path/dist"

    if (osName.contains("win")) {
        commandLine "cmd", "/c", "jppm.bat", "github:publish", "-f", "-y"
    } else {
        commandLine "sh", "jppm", "github:publish", "-f", "-y"
    }
}


task installDist(dependsOn: 'dist') {
    doLast {
        def installDist = file(System.getProperty('user.home') + '/.jppm/dist')
        installDist.mkdirs()

        copy {
            from "$buildDir.path/dist/buildSrc/"
            into "$installDist.path/buildSrc"
        }

        copy {
            from "$buildDir.path/dist/jppm"
            from "$buildDir.path/dist/jppm.bat"
            from "$buildDir.path/dist/packager-all.jar"
            from "$buildDir.path/dist/package.php.yml"

            into installDist
        }

        def osName = System.getProperty("os.name").toLowerCase()
        file("$installDist/jppm").setExecutable(true)

        if (osName.contains("linux") || osName.contains("unix")) {
            file("/usr/bin/jppm").delete()
            file("/usr/bin/jppm").deleteOnExit()

            exec {
                commandLine "ln", "-sfn", "$installDist/jppm", "/usr/local/bin"
            }
        }
    }
}

install {
    dependsOn installDist
}
