apply plugin: 'maven'

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['src-php']
}

dependencies {
    compile project(':jphp-core')
    compile project(':exts/jphp-zend-ext')
    compile project(':exts/jphp-yaml-ext')
    compile project(':exts/jphp-compress-ext')
    compile project(':exts/jphp-json-ext')
    compile project(':exts/jphp-httpserver-ext')
    compile project(':exts/jphp-semver-ext')
}

shadowJar {
    archiveName = 'packager-all.jar'

    mergeServiceFiles()
}

task dist(dependsOn: 'shadowJar') {
    doLast {
        copy {
            from "$buildDir.path/libs/$shadowJar.archiveName"
            from "$projectDir.path/scripts/"

            into "$buildDir.path/dist/"
        }

        copy {
            from "$projectDir.path/buildSrc/"
            into "$buildDir.path/dist/buildSrc"
        }
    }
}


task distArch(type: Tar, dependsOn: ['clean', 'dist', 'distArchZip']) {
    from("$buildDir.path/dist") {
        exclude('jppm')
    }

    from("$buildDir.path/dist") {
        include('jppm')

        fileMode = 0755
    }

    baseName = "jppm-dist"
    destinationDir = buildDir
    extension = 'tar.gz'

    compression = Compression.GZIP
}

task distArchZip(type: Zip, dependsOn: 'dist') {
    from("$buildDir.path/dist")

    baseName = "jppm-dist"
    destinationDir = buildDir
    extension = 'zip'
}


task installDist(dependsOn: 'dist') {
    doLast {
        def installDist = file(System.getProperty('user.home') + '/.jppm/dist')
        installDist.mkdirs()

        copy {
            from "$buildDir.path/dist/buildSrc/"
            into "$installDist.path/buildSrc"
        }

        copy {
            from "$buildDir.path/dist/jppm"
            from "$buildDir.path/dist/jppm.bat"
            from "$buildDir.path/dist/packager-all.jar"

            into installDist
        }

        def osName = System.getProperty("os.name").toLowerCase()
        file("$installDist/jppm").setExecutable(true)

        if (osName.contains("linux") || osName.contains("unix")) {
            exec {
                commandLine "ln", "-sfn", "$installDist/jppm", "/usr/bin/jppm"
            }
        }
    }
}

install {
    dependsOn installDist
}
